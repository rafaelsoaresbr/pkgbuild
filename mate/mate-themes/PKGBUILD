# Maintainer : Martin Wimpress <code@flexion.org>

_ver=3.20
pkgname=mate-themes
pkgver=${_ver}.5
pkgrel=2
pkgdesc="Default themes for the MATE desktop"
url="http://mate-desktop.org"
arch=('any')
license=('GPL')
depends=('gtk-engines' 'gtk-engine-murrine' 'librsvg' 'mate-icon-theme')
makedepends=('mate-common' 'icon-naming-utils')
groups=('mate' 'mate-gtk3')
source=("http://pub.mate-desktop.org/releases/themes/${_ver}/${pkgname}-${pkgver}.tar.xz"
        "fix-selection.patch")
sha1sums=('640d33c45d37247a9e27de15df8685cfaa8c951d'
          'da38d1a46be209d81cfd69e4ab3f579bde2c6d20')
install=${pkgname}.install

prepare() {
    cd "${srcdir}/${pkgname}-${pkgver}"

    # GTK+-3.20: fix text selection in firefox
    patch -Np1 -i ../fix-selection.patch
}

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
