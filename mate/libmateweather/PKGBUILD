# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.12
pkgbase=libmateweather
pkgname=${pkgbase}-gtk3
pkgver=${_ver}.1
pkgrel=1
pkgdesc="Provides access to weather information from the Internet. (GTK3 version [EXPERIMENTAL])"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('LGPL')
depends=('dconf' 'gtk3' 'libsoup')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('dfd58b8fa7ec93e04773f61006b34b21a08e66d2')
install=${pkgbase}.install

build() {
    cd "${srcdir}/${pkgbase}"
    ./configure \
        --prefix=/usr \
        --sysconfdir=/etc \
        --localstatedir=/var \
        --with-gtk=3.0 \
        --disable-static \
        --disable-python \
        --enable-locations-compression
    make
}

package() {
    cd "${srcdir}/${pkgbase}"
    make DESTDIR="${pkgdir}" install
}
