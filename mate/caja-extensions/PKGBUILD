# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.15
pkgbase=caja-extensions
pkgname=(caja-gksu caja-image-converter caja-open-terminal caja-sendto caja-share)
pkgver=${_ver}.0
pkgrel=1
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('caja>=1.12' 'dbus-glib' 'gksu' 'gtk3' 'gupnp' 'imagemagick' 'samba')
makedepends=('mate-common')
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgbase}-${pkgver}.tar.xz")
sha1sums=('f4299e99fe889038834ab9743cf6418f01a777cc')

build() {
    cd "${srcdir}/${pkgbase}-${pkgver}"
    PYTHON=/usr/bin/python2 ./configure \
        --prefix=/usr \
        --with-gtk=3.0
    make
}

package_caja-extensions-common() {
    pkgdesc="Caja extensions (common files)"
    depends=()

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C po DESTDIR="${pkgdir}" install
}

package_caja-gksu() {
    pkgdesc="Privilege granting extension for Caja using gksu"
    depends=('caja' 'caja-extensions-common' 'gksu')
    groups=('mate-extra')
    conflicts=('caja-gksu')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C gksu DESTDIR="${pkgdir}" install
}

package_caja-image-converter() {
    pkgdesc="A Caja extension for simple image conversions"
    depends=('caja' 'caja-extensions-common' 'imagemagick')
    groups=('mate-extra')
    conflicts=('caja-image-converter')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C image-converter DESTDIR="${pkgdir}" install
}

package_caja-open-terminal() {
    pkgdesc="A Caja extension for opening terminals in arbitrary local paths"
    depends=('caja' 'caja-extensions-common')
    groups=('mate-extra')
    conflicts=('caja-open-terminal')
    install=caja-open-terminal.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C open-terminal DESTDIR="${pkgdir}" install
}

package_caja-sendto() {
    pkgdesc="A Caja extension for sending files"
    depends=('caja' 'caja-extensions-common' 'dbus-glib' 'gupnp')
    groups=('mate-extra')
    optdepends=('pidgin: Support for sending to instant messaging networks.'
                'gajim: Support for sending to Jabber.')
    conflicts=('caja-sendto')
    install=caja-sendto.install

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C sendto DESTDIR="${pkgdir}" install
}

package_caja-share() {
    pkgdesc="A Caja extension to quickly share a folder"
    depends=('caja' 'caja-extensions-common' 'samba')
    groups=('mate-extra')
    conflicts=('caja-share')

    cd "${srcdir}/${pkgbase}-${pkgver}"
    make -C share DESTDIR="${pkgdir}" install
}
