# $Id$
# Maintainer : Martin Wimpress <code@flexion.org>

_ver=1.15
pkgname=atril
pkgver=${_ver}.3
pkgrel=1
pkgdesc="MATE document viewer"
url="http://mate-desktop.org"
arch=('i686' 'x86_64')
license=('GPL')
depends=('dconf' 'dbus' 'desktop-file-utils' 'gtk3' 'libsecret' 'libsm' 'libspectre'
         'mate-desktop>=1.15' 'mathjax' 'poppler-glib' 'webkit2gtk' 'zlib')
makedepends=('caja>=1.15' 'djvulibre' 'gobject-introspection' 'libgxps'
             'mate-common' 'texlive-bin' 'yelp-tools')
optdepends=('caja: Document Properties extension'
            'djvulibre: DjVu support'
            'libgxps: XPS support'
            'texlive-bin: DVI support'
            'yelp: for reading MATE help documents')
groups=('mate-extra')
conflicts=("${pkgname}")
source=("http://pub.mate-desktop.org/releases/${_ver}/${pkgname}-${pkgver}.tar.xz")
sha1sums=('d13011111732e7db6196702e66183c19dd1828b4')

build() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    ./configure \
        --prefix=/usr \
        --libexecdir=/usr/lib/${pkgname} \
        --with-gtk=3.0 \
        --enable-gtk-doc \
        --enable-djvu \
        --enable-dvi \
        --enable-epub \
        --enable-t1lib \
        --enable-pixbuf \
        --enable-comics \
        --enable-xps \
        --enable-introspection \
        --disable-static
    make
}

package() {
    cd "${srcdir}/${pkgname}-${pkgver}"
    make DESTDIR="${pkgdir}" install
}
